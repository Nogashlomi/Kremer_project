PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sphaera: <http://ontologies.mpiwg-berlin.mpg.de/sphaera/>
PREFIX sphType:<http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/>
PREFIX sp: <http://spinrdf.org/sp#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rs: <http://www.researchspace.org/ontology/>
PREFIX crmDig: <http://www.ics.forth.gr/isl/CRMdig/>


SELECT DISTINCT ?cluster_name ?note  (GROUP_CONCAT(DISTINCT ?author_name; SEPARATOR=";") as ?authors) (GROUP_CONCAT(DISTINCT ?publisher; SEPARATOR=";") as ?publishers) (GROUP_CONCAT(DISTINCT ?printer; SEPARATOR=";") as ?printers) ?place (year(?begin) as ?year) ?bid ?pagenum ?bb
WHERE {
  	# Choose Cluster by replacing SIL_00230 with any other cluster name.
  	# ?cluster ecrm:P3_has_note "SIL_00230".
  
  
  	
  	?cluster a sphaera:Element.
  	?cluster ecrm:P3_has_note ?cluster_name.
  	


    # ?cluster ecrm:P2_has_type <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/cluster-keyword/92efcdad-6b8b-498a-8464-3ed716b73064> .
   
   
    ######### ADD NAME OF CLUSTER HERE ##################
  ?cluster ecrm:P2_has_type/ecrm:P3_has_note ?note .
  
    
  OPTIONAL{
  	?cluster ^ecrm:P138_represents ?images.
    ?images rs:boundingBox ?bb.
    ?images crmDig:L49_is_primary_area_of ?page.
    ?page ecrm:P1_is_identified_by ?pagenum.
    
    ?page ^ecrm:P106_is_composed_of/^ecrm:P128_carries/^efrbroo:R30_produced/^efrbroo:R29i_was_reproduced_by ?book.
    
    ?book a sphaera:Book;
		ecrm:P1_is_identified_by ?idtype.
  	?idtype ecrm:P2_has_type <http://ontologies.mpiwg-berlin.mpg.de/sphaera/customIdentifier>.
  	?idtype ecrm:P3_has_note ?bid.
    
    
    OPTIONAL{
        ?book ecrm:P128_carries/ecrm:P165_incorporates/efrbroo:R17i_was_created_by/ecrm:PC01_is_domain_of ?author.
        ?author ecrm:P14.1_in_the_role_of <http://sphaera.mpiwg-berlin.mpg.de/id/thes/role/author>;
            ecrm:PC02_has_range/ecrm:P3_has_note ?author_name.
      }
     OPTIONAL{
	?book ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P3_has_note ?publisher.
    }
    OPTIONAL{
        ?book efrbroo:R28i_was_produced_by/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P3_has_note ?printer.
    }
    OPTIONAL{
        ?book ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P7_took_place_at/ecrm:P3_has_note ?place.
    }
    OPTIONAL{
        ?book ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P4_has_time-span/ecrm:P82a_begin_of_the_beginning ?begin.
    } 
  } FILTER (?note = "CK_12 Spheres"|| ?note = "CK_Armillary Sphere") 
}
GROUP BY ?cluster_name ?note  ?bid ?place ?begin ?bb ?pagenum 
ORDER BY ?cluster_name ?bid ?pagenum