PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sphaera: <http://ontologies.mpiwg-berlin.mpg.de/sphaera/>
PREFIX sphType:<http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/>
PREFIX sp: <http://spinrdf.org/sp#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rs: <http://www.researchspace.org/ontology/>
PREFIX crmDig: <http://www.ics.forth.gr/isl/CRMdig/>


SELECT ?images ?cluster_name (GROUP_CONCAT(DISTINCT ?note; SEPARATOR=", ") as ?cks) ?book ?bid ?part_or_adaption ?part_or_adaption_label ?custom_identifier ?place (year(?begin)as ?year) ?pagenum ?part_or_adaption_type
WHERE {
  	
  	?cluster a sphaera:Element.
  	?cluster ecrm:P3_has_note ?cluster_name.
  ?cluster ecrm:P2_has_type/ecrm:P3_has_note ?note .
  	?cluster ^ecrm:P138_represents ?images.
  ?images crmDig:L49_is_primary_area_of ?page.
  ?page ecrm:P1_is_identified_by ?pagenum.
  ?page ^ecrm:P106_is_composed_of/^ecrm:P128_carries/^efrbroo:R30_produced/^efrbroo:R29i_was_reproduced_by ?book.
  
  ?book a sphaera:Book;
		ecrm:P1_is_identified_by ?idtype.
  	?idtype ecrm:P2_has_type <http://ontologies.mpiwg-berlin.mpg.de/sphaera/customIdentifier>.
  	?idtype ecrm:P3_has_note ?bid.

        OPTIONAL{
  ?attribute_assignment_cha ecrm:P140_assigned_attribute_to/^ecrm:P165_incorporates/^ecrm:P128_carries ?book;
  ecrm:P2_has_type sphType:chapterAssignment.
    	?attribute_assignment_cha ecrm:P141_assigned ?chapter_temp.
  	?chapter_temp ecrm:P3_has_note ?chapter.
     ?attribute_assignment_cha sphaera:pageFrom ?p1.
  ?attribute_assignment_cha sphaera:pageTo ?p2.
  }
  
  
     OPTIONAL {
  ?attribute_assignment ecrm:P2_has_type sphType:partAssignment;
    ecrm:P140_assigned_attribute_to/^ecrm:P165_incorporates/^ecrm:P128_carries ?book ;
    ecrm:P141_assigned ?part_or_adaption ;
    sphaera:pageFrom ?page_from ;
    sphaera:pageTo ?page_to .
  FILTER(?page_from <= ?pagenum && ?page_to >= ?pagenum)
  ?part_or_adaption ecrm:P3_has_note ?part_or_adaption_label ;
    a ?part_or_adaption_type .
    ?part_or_adaption  ecrm:P1_is_identified_by ?part_id.
   ?part_id ecrm:P2_has_type sphaera:customIdentifier ;
  ecrm:P3_has_note ?custom_identifier_str .
BIND(xsd:integer(?custom_identifier_str) as ?custom_identifier)
  VALUES (?part_or_adaption_type) {
    (sphaera:Part)
    (sphaera:Part_Adaption)
  }
}
  
   OPTIONAL{
        ?book ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P7_took_place_at/ecrm:P3_has_note ?place.
    }
     OPTIONAL{
        ?book ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P4_has_time-span/ecrm:P82a_begin_of_the_beginning ?begin.
    } 
  
#place year
 
  FILTER (regex(?note, '^CK_'))
  FILTER (regex(?cluster_name, '^SAC_SIL'))
} GROUP BY ?images ?cluster_name ?book ?bid ?part_or_adaption ?part_or_adaption_label ?custom_identifier ?place ?begin ?pagenum ?part_or_adaption_type
