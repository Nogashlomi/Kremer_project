PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sphaera: <http://ontologies.mpiwg-berlin.mpg.de/sphaera/>
PREFIX sphType:<http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/>
PREFIX sp: <http://spinrdf.org/sp#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rs: <http://www.researchspace.org/ontology/>
PREFIX crmDig: <http://www.ics.forth.gr/isl/CRMdig/>

SELECT DISTINCT 
?custom_identifier
?subject
?title
?label
?period
(GROUP_CONCAT(DISTINCT ?dataset_label; SEPARATOR=", ") as ?dataset_labels)
(GROUP_CONCAT(DISTINCT ?corpus_label; SEPARATOR=", ") as ?corpora_labels)
(GROUP_CONCAT(DISTINCT ?author_label; SEPARATOR=", ") as ?authors)
(GROUP_CONCAT(DISTINCT ?original_author_label; SEPARATOR=", ") as ?original_authors)
(GROUP_CONCAT(DISTINCT ?sort_author_name; SEPARATOR=", ") as ?sort_authors)
(GROUP_CONCAT(DISTINCT ?adaption_type; SEPARATOR="/ ") as ?adaption_types)
(COUNT(DISTINCT ?book) as ?num_books)
(MIN(YEAR(?book_date)) as ?date)

WHERE{
?subject a sphaera:Part_Adaption ;
  ecrm:P3_has_note ?label ;
  ecrm:P2_has_type/ecrm:P3_has_note ?adaption_type .

OPTIONAL{
  ?subject ecrm:P4_has_time-span ?period_uri .
  ?period_uri ecrm:P82_at_some_point_within ?period .
}

OPTIONAL {
  ?subject ecrm:P1_is_identified_by ?identifier .
  ?identifier ecrm:P2_has_type sphaera:customIdentifier ;
  ecrm:P3_has_note ?custom_identifier_str .
  BIND(xsd:integer(?custom_identifier_str) as ?custom_identifier)
}

OPTIONAL{
  ?subject ^sphaera:partadaptionlink ?book .

}

OPTIONAL{
  ?subject ^ecrm:P15_was_influenced_by/^efrbroo:R17i_was_created_by ?part .   
  ?part ^sphaera:partadaptionlink ?book .
}

OPTIONAL {
  ?book ecrm:P128_carries/ecrm:P165_incorporates/ecrm:P148_has_component/efrbroo:R15i_is_fragment_of? ?subject ;
    ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P4_has_time-span/ecrm:P82a_begin_of_the_beginning ?book_date .
}
  
OPTIONAL {
  ?book ecrm:P128_carries/ecrm:P165_incorporates/ecrm:P148_has_component ?subject .
}

OPTIONAL {
  ?book ecrm:P12_occurred_in_the_presence_of ?dataset .
  <http://sphaera.mpiwg-berlin.mpg.de/id/thes/dataset> <http://www.w3.org/2004/02/skos/core#member> ?dataset .
  ?dataset ecrm:P3_has_note ?dataset_label .
}

OPTIONAL {
  ?book ecrm:P12_occurred_in_the_presence_of ?corpus .
  <http://sphaera.mpiwg-berlin.mpg.de/id/thes/corpus> <http://www.w3.org/2004/02/skos/core#member> ?corpus .
  ?corpus ecrm:P3_has_note ?corpus_label .
}

OPTIONAL {
  ?subject efrbroo:R17i_was_created_by/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P48_has_preferred_identifier/ecrm:P3_has_note ?author_label .
  #[[> :queryFormatName input="?author_label" output="?author_name"]]
  BIND(LCASE(?author_label) AS ?sort_author_name)
}

OPTIONAL{
  ?subject ((ecrm:P106_is_composed_of/ecrm:P165_incorporates)|(ecrm:P165_incorporates/efrbroo:R3i_realises/^efrbroo:R40i_is_representative_expression_for)) ?original_part .
  ?original_part a sphaera:Part ;
    efrbroo:R17i_was_created_by/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P48_has_preferred_identifier/ecrm:P3_has_note ?original_author_label .
   #[[> :queryFormatName input="?original_author_label" output="?original_author_name"]]
	}} GROUP BY ?subject
?title
?custom_identifier
?label
?period



