PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
    PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
    PREFIX ecrm: <http://erlangen-crm.org/current/>
    PREFIX owl: <http://www.w3.org/2002/07/owl#>
    PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
    PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
    PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
    PREFIX sphaera: <http://ontologies.mpiwg-berlin.mpg.de/sphaera/>
    PREFIX sphType:<http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/>
    PREFIX sp: <http://spinrdf.org/sp#>
    PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
    PREFIX rs: <http://www.researchspace.org/ontology/>
    PREFIX crmDig: <http://www.ics.forth.gr/isl/CRMdig/>
    
    
    SELECT DISTINCT ?cluster_name ?keyword ?chapter (COUNT(DISTINCT ?images) as ?selected_images) (COUNT(DISTINCT ?allimages) as ?all_images_in_cluster)
    WHERE {
      
      
          ?part a sphaera:Part.
        ?part ecrm:P1_is_identified_by ?id.
          ?id ecrm:P2_has_type <http://ontologies.mpiwg-berlin.mpg.de/sphaera/customIdentifier>.
          # Change part ID HERE - Change 100 with desi
          ?id ecrm:P3_has_note "100".
      
      
          ?cluster a sphaera:Element.
          ?cluster ecrm:P3_has_note ?cluster_name.
          ?cluster ^ecrm:P138_represents ?images.
          ?images crmDig:L49_is_primary_area_of ?page.
          ?page ecrm:P1_is_identified_by ?pagenum.
      
          ?cluster ^ecrm:P138_represents ?allimages.
      
      
          OPTIONAL{
          ?cluster ecrm:P2_has_type/ecrm:P3_has_note ?keyword.
        }
          
          ?page ^ecrm:P106_is_composed_of/^ecrm:P128_carries/^efrbroo:R30_produced/^efrbroo:R29i_was_reproduced_by/ecrm:P128_carries/ecrm:P165_incorporates ?temp.
        ?temp ecrm:P148_has_component ?part.
          ?temp ^ecrm:P140_assigned_attribute_to ?attribute_main.
      
          ?attribute_main ecrm:P2_has_type <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/chapterAssignment>.
          ?attribute_main ecrm:P141_assigned ?chapter_temp.
          ?chapter_temp ecrm:P3_has_note ?chapter.
          ?chapter_temp ^ecrm:P106_is_composed_of ?part.
          
          ?attribute_main sphaera:pageFrom ?p1.
          ?attribute_main sphaera:pageTo ?p2.
      
      FILTER(?pagenum >= ?p1)
      FILTER(?pagenum <= ?p2)
    }
    
    GROUP BY ?cluster_name ?keyword ?chapter
    ORDER BY ?cluster_name