PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX sphaera: <http://ontologies.mpiwg-berlin.mpg.de/sphaera/>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX sphType: <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT 
?book
?book_label
?bid
?custom_identifier
?label
?subject
?title
?period
(GROUP_CONCAT(DISTINCT ?part_type_label; SEPARATOR=", ") as ?part_type_labels)
(GROUP_CONCAT(DISTINCT ?dataset_label; SEPARATOR=", ") as ?dataset_labels)
(GROUP_CONCAT(DISTINCT ?corpus_label; SEPARATOR=", ") as ?corpora_labels)

 WHERE {
  VALUES ?part_or_adaption {sphaera:Part sphaera:Part_Adaption}
  ?subject a ?part_or_adaption;
  ecrm:P3_has_note ?label ;
  ecrm:P1_is_identified_by ?identifier .
   ?subject ecrm:P2_has_type ?part_type .
   ?part_type ecrm:P3_has_note ?part_type_label.


?identifier ecrm:P2_has_type sphaera:customIdentifier ;
  ecrm:P3_has_note ?custom_identifier_str .
BIND(xsd:integer(?custom_identifier_str) as ?custom_identifier)

OPTIONAL{
  ?subject ecrm:P4_has_time-span ?period_uri.
  ?period_uri ecrm:P82_at_some_point_within ?period.
}

OPTIONAL{
  ?subject ((^sphaera:partlink)|(^sphaera:partadaptionlink) | (^ecrm:P15_was_influenced_by/^efrbroo:R17i_was_created_by/((^sphaera:partadaptionlink) | (^sphaera:partlink)))) ?book .
    
  ?book ecrm:P3_has_note ?book_label.
   ?book ecrm:P1_is_identified_by ?book_identifier.
    ?book_identifier ecrm:P2_has_type sphaera:customIdentifier ;
  ecrm:P3_has_note ?book_custom_identifier_str .
BIND(xsd:integer(?book_custom_identifier_str) as ?bid)
    

  ?book ecrm:P12_occurred_in_the_presence_of ?dataset .
  <http://sphaera.mpiwg-berlin.mpg.de/id/thes/dataset> <http://www.w3.org/2004/02/skos/core#member> ?dataset .
  ?dataset ecrm:P3_has_note ?dataset_label .

  ?book ecrm:P12_occurred_in_the_presence_of ?corpus .
  <http://sphaera.mpiwg-berlin.mpg.de/id/thes/corpus> <http://www.w3.org/2004/02/skos/core#member> ?corpus .
  ?corpus ecrm:P3_has_note ?corpus_label .
}

OPTIONAL {
  ?subject ecrm:PC01_is_domain_of ?has_title .
  ?has_title a ecrm:PC102_has_title ;
    ecrm:P102.1_has_type sphType:primary ;
    ecrm:PC02_has_range/ecrm:P3_has_note ?part_title .
}
BIND( IF(BOUND(?part_title), ?part_title, "&nbsp;") as ?title )
  FILTER (?bid < 5000)
} GROUP BY 
?book
?book_label
?bid
?subject 
?title 
?custom_identifier 
?label 
?period
ORDER BY ?bid ?custom_identifier 
