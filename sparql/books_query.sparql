PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sphaera: <http://ontologies.mpiwg-berlin.mpg.de/sphaera/>
PREFIX sphType:<http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/>
PREFIX sp: <http://spinrdf.org/sp#>
PREFIX crm: <http://www.cidoc-crm.org/cidoc-crm/>
PREFIX rs: <http://www.researchspace.org/ontology/>
PREFIX crmDig: <http://www.ics.forth.gr/isl/CRMdig/>


SELECT DISTINCT ?book ?label ?bid (GROUP_CONCAT(DISTINCT ?author_name; SEPARATOR=';') as ?authors) (GROUP_CONCAT(DISTINCT ?publisher; SEPARATOR=';') as ?publishers) (GROUP_CONCAT(DISTINCT ?printer; SEPARATOR=';') as ?printers) ?place (year(?begin) as ?year) (count(distinct ?page) as ?num_pages)
WHERE{
	?book a sphaera:Book ;
  ecrm:P3_has_note ?label .
	?book ecrm:P128_carries/ecrm:P165_incorporates/efrbroo:R17i_was_created_by/ecrm:PC01_is_domain_of ?author.
    ?author ecrm:P14.1_in_the_role_of <http://sphaera.mpiwg-berlin.mpg.de/id/thes/role/author>;
         ecrm:PC02_has_range/ecrm:P3_has_note ?author_name.

      OPTIONAL {
    ?book ecrm:P1_is_identified_by ?identifier .
    ?identifier ecrm:P2_has_type sphaera:customIdentifier ;
      ecrm:P3_has_note ?custom_identifier_str .
      BIND(xsd:integer(?custom_identifier_str) as ?bid)
  }

    ?book efrbroo:R29i_was_reproduced_by/efrbroo:R30_produced/ecrm:P128_carries/ecrm:P106_is_composed_of ?page . 
  
        OPTIONAL {
    ?page ecrm:P2_has_type ?page_label.
    
    BIND(
  IF (?page_label = <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/page-keyword/30af573a-8d8b-11ec-b7b2-acde48001122>, 'true', 	  # padding
      IF (?page_label = <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/page-keyword/30af7cec-8d8b-11ec-b7b2-acde48001122>, 'true',    # duplicate
        IF (?page_label = <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/page-keyword/30af4ac4-8d8b-11ec-b7b2-acde48001122>, 'true', 	  # digit. artefact
          IF (?page_label = <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/page-keyword/c2462315-61c3-4f27-8735-a49c0b18a83a>, 'true', 	  # manually added leaf
             IF (?page_label = <http://sphaera.mpiwg-berlin.mpg.de/id/thes/type/page-keyword/a7e4685e-94a3-4c23-8314-0e95baf4b31f>, 'true', '')	  # text added by buyer

          )
        )
      )

    
  )
  AS ?flag
    )
  }
     OPTIONAL{
	?book ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P3_has_note ?publisher.
    }
    OPTIONAL{
       ?book efrbroo:R28i_was_produced_by/ecrm:PC01_is_domain_of/ecrm:PC02_has_range/ecrm:P3_has_note ?printer.
    }
    OPTIONAL{
        ?book ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P7_took_place_at/ecrm:P3_has_note ?place.
    }
    OPTIONAL{
        ?book ecrm:P128_carries/efrbroo:R24i_was_created_through/ecrm:P4_has_time-span/ecrm:P82a_begin_of_the_beginning ?begin.
    } 
FILTER (!BOUND(?flag) || STRLEN(?flag) < 1)
FILTER(xsd:integer(?bid) < 3000)
} GROUP BY ?book ?label ?bid ?place ?begin 